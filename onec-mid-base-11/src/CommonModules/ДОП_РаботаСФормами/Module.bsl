#Область ПрограммныйИнтерфейс
//Дополнение форм документов реквизитами КОнтактноеЛицо и СогласованнаяСкидка 
Процедура ДополнитьФорму(Форма) Экспорт 
//{{Колесников Р.В. 11.10.2023
//Процедура для определения вида форм и вставки необходимого элемента на форму
	
	ИмяФормы = Форма.ИмяФормы; 
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
    ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммойДокумента(Форма);    
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммойДокумента(Форма); 
	КонецЕсли;
	
КонецПроцедуры
 //}}   
 
#КонецОбласти

#Область СлужебныйПРограммныйИнтерфейс 

Процедура ДобавитьКомандуРассчитатьНаФорму(Форма) Экспорт
//{{Колесников Р.В. 12.12.2023: Добавление команды о перерасчёте строк таблицы с указанной скидкой в поле согласованная скидка 
//Добавление команды формы 

	Команда = Форма.Команды.Добавить("Рассчитать");
	Команда.Заголовок = "Рассчитать";
	Команда.Действие = "КомандаРассчитатьСтроки";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаРассчитать", Тип("КнопкаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	КнопкаФормы.ИмяКоманды = "Рассчитать";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
//}}	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
//{{ПРоцедура для добавления КОнтактного лица в шапку форм
//Колесников Р.В. 12.12.2023
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДОП_КонтактноеЛицо";
//}}	
КонецПроцедуры 

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма)
//{{ПРоцедура для добавления Согсласованной скидки в шапку форм
//Колесников Р.В. 12.12.2023
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДОП_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзмененииДанных");
//}}	
КонецПроцедуры 


Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммойДокумента(Форма) 
//{{ПРоцедура для добавления КОнтактного лица в подвал форм
//Колесников Р.В. 12.12.2023
	Если Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),,Форма.Элементы.Основание);
	Иначе
		ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),,Форма.Элементы.СуммаДокумента);
	КонецЕсли;
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДОП_КонтактноеЛицо";
//}}	
КонецПроцедуры

#КонецОбласти
